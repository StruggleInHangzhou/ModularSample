apply plugin: 'com.android.library'
apply plugin: 'maven'

def projectName = project.getName()

android {
    compileSdkVersion Integer.parseInt(COMPILE_SDK_VERSION)

    compileOptions {
        sourceCompatibility rootProject.ext.javaVersion
        targetCompatibility rootProject.ext.javaVersion
    }

    dataBinding {
        enabled true
    }

    defaultConfig {
        minSdkVersion Integer.parseInt(MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(TARGET_SDK_VERSION)
        versionCode Integer.parseInt(CORE_VERSION)
        versionName CORE_VERSION_NAME

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: projectName]
                includeCompileClasspath = true
            }
        }

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$rootProject.leakcanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$rootProject.leakcanaryVersion"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$rootProject.leakcanaryVersion"
    //noinspection GradleDependency
    api "com.android.support:appcompat-v7:${SUPPORT_LIB_VERSION}"
    api "com.android.support:recyclerview-v7:${SUPPORT_LIB_VERSION}"
    api "com.android.support:cardview-v7:${SUPPORT_LIB_VERSION}"
    api "com.android.support:design:${SUPPORT_LIB_VERSION}"
    api "com.android.support.constraint:constraint-layout:${CONSTRAINT_LAYOUT}"
    api "com.android.support:multidex:$rootProject.multidexVersion"
    api "com.alibaba:arouter-api:$rootProject.arouterApiVersion"
    annotationProcessor "com.alibaba:arouter-compiler:$rootProject.arouterCompilerVersion"

    api "com.orhanobut:logger:$rootProject.loggerVersion"
    api "com.google.dagger:dagger:$rootProject.daggerVersion"
    api "com.google.dagger:dagger-android:$rootProject.daggerAndroidVersion"
    api "com.google.dagger:dagger-android-support:$rootProject.daggerAndroidSupportVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.daggerCompilerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$rootProject.daggerAndroidProcessorVersion"

    api "io.reactivex.rxjava2:rxjava:$rootProject.rxjava2Version"
    api "io.reactivex.rxjava2:rxandroid:$rootProject.rxandroid2Version"
    api "com.jakewharton.rxbinding2:rxbinding:$rootProject.rxbinding2Version"
    api "com.jakewharton.rxbinding2:rxbinding-support-v4:$rootProject.rxbinding2SupportV4Version"
    api "com.jakewharton.rxbinding2:rxbinding-appcompat-v7:$rootProject.rxbinding2AppcompatV7Version"
    api "com.jakewharton.rxbinding2:rxbinding-design:$rootProject.rxbinding2DesignVersion"
    api "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:$rootProject.rxbinding2recyclerviewV7Version"
    api "com.jakewharton.rxrelay2:rxrelay:$rootProject.rxrelay2Version"
    api "com.trello.rxlifecycle2:rxlifecycle:$rootProject.rxlifecycle2Version"
    api "com.trello.rxlifecycle2:rxlifecycle-android:$rootProject.rxlifecycle2AndroidVersion"
    api "com.trello.rxlifecycle2:rxlifecycle-components:$rootProject.rxlifecycle2ComponentsVersion"
    api "com.tbruyelle.rxpermissions2:rxpermissions:$rootProject.rxpermissions2Version"

    api "com.blankj:utilcode:$rootProject.utilcodeVersion"

    api "me.yokeyword:fragmentation:$rootProject.fragmentationVersion"
    api "me.yokeyword:fragmentation-swipeback:$rootProject.fragmentationSwipebackVersion"


    if (httpRelease.toBoolean()) {
        api "com.gavel:http:$HTTP_VERSION_NAME"
    } else {
        api project(':http')
    }
    if (dbRelease.toBoolean()) {
        api "com.gavel:db:$DB_VERSION_NAME"
    } else {
        api project(':db')
    }
    if (basicRelease.toBoolean()) {
        api "com.gavel:basic:$BASIC_VERSION_NAME"
    } else {
        api project(':basic')
    }
}

uploadArchives {
    configuration = configurations.archives
    repositories {
        mavenDeployer {
            repository(url: MAVEN_REPO_URL) {
                authentication(userName: NEXUS_USERNAME, password: NEXUS_PASSWORD)
            }
            pom.project {
                version CORE_VERSION_NAME
                artifactId projectName
                groupId GROUP_ID
                packaging TYPE
                description DESCRIPTION
            }
        }
    }
}

artifacts {
    archives file(projectName + ".aar")
}
